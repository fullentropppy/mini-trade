
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль объекта
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

// Обработчик события Обработка проведения.
//
Процедура ОбработкаПроведения(Отказ, Режим)
		
	// Создание сгруппированного списка товаров.
	СписокТоваров = Товары.Выгрузить();
	СписокТоваров.Свернуть("Товар, Цена", "Количество, Сумма");
	
	ЗаписатьДвиженияВОстаткиТоваровИПродажиТоваров(СписокТоваров);
	
	Если Не ДополнительныеСвойства.Свойство("ПроведениеБезКонтроляОстатков") Тогда
		ПроверяемыеТовары = СписокТоваров.ВыгрузитьКолонку("Товар");
		ЗапасыСервер.ПроконтролироватьОстаткиТоваров(ПроверяемыеТовары, Склад, МоментВремени(), Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		// Указание необходимости записи движений в регистр накопления 
		// ПродажиТоваров после успешного прохождения контроля остатков.
		Движения.ПродажиТоваров.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Записывает движения документа в регистр накопления Остатки товаров и Продажа товаров.
//
// Параметры:
//  СписокТоваров - ТаблицаЗначений - таблица товаров. 
//
Процедура ЗаписатьДвиженияВОстаткиТоваровИПродажиТоваров(СписокТоваров)
	
	Движения.ОстаткиТоваров.Записывать 				= Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	
	Для Каждого СтрокаТоваров Из СписокТоваров Цикл
		// Регистр накопления ОстаткиТоваров.
		Движение 			= Движения.ОстаткиТоваров.ДобавитьРасход();
		Движение.Период 	= Дата;
		Движение.Склад 		= Склад;
		Движение.Товар 		= СтрокаТоваров.Товар;
		Движение.Количество = СтрокаТоваров.Количество;
		
		// Регистр накопления Продажи.
		Движение 			= Движения.ПродажиТоваров.Добавить();
		Движение.Период 	= Дата;
		Движение.Склад 		= Склад;
		Движение.Товар 		= СтрокаТоваров.Товар;
		Движение.Количество = СтрокаТоваров.Количество;
		Движение.Сумма 		= СтрокаТоваров.Сумма;
	КонецЦикла;
	
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти
