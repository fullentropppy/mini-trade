
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события При создании на сервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДанныеДинамическогоСписка();		
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

// Обработчик события Список выбор.
//
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;	
	Если Не ТекущиеДанные = Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает запрос и параметры запроса динамического списка Список.
//
&НаСервере
Процедура УстановитьДанныеДинамическогоСписка()
	
	Документ 			=  Параметры.Отбор.Значение;
	МетаданныеДокумента	= Документ.Метаданные();
	
	Если Не МетаданныеДокумента.Реквизиты.Найти("Основание") = Неопределено 
	 И ЗначениеЗаполнено(Документ.Основание) Тогда
	 	// Если у документа есть реквизит Основание и значение реквизита заполнено,
		// то в результат критерия отбора будет добавлена ссылка основания.
		ИмяДокумента 	= МетаданныеДокумента.ПолноеИмя();
		ТекстЗапроса	= ТекстЗапросаДинамическогоСписка(Истина);
		ТекстЗапроса 	= СтрЗаменить(ТекстЗапроса, "&ИмяТекущегоДокумента", ИмяДокумента);
	Иначе
		ТекстЗапроса = ТекстЗапросаДинамическогоСписка(Ложь);	
	КонецЕсли;
	
	Список.ТекстЗапроса	= ТекстЗапроса;
	Список.Параметры.УстановитьЗначениеПараметра("Документ", Документ);
	
КонецПроцедуры

// Возвращает текст запроса для динамического списка Список.
//
// Параметры:
//  ЕстьРеквизитОснование - Булево - признак того, что документ, для которого 
//									 выводятся связанные документы, имеет реквизит Основание.
// 
// Возвращаемое значение:
//   - Строка
//
&НаСервереБезКонтекста
Функция ТекстЗапросаДинамическогоСписка(ЕстьРеквизитОснование)
	
	ОсновнойТекст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА КритерийОтбораСвязанныеДокументы.Ссылка.Проведен
		|			ТОГДА 14
		|		КОГДА КритерийОтбораСвязанныеДокументы.Ссылка.ПометкаУдаления
		|			ТОГДА 13
		|		ИНАЧЕ 12
		|	КОНЕЦ КАК Состояние,
		|	КритерийОтбораСвязанныеДокументы.Ссылка КАК Ссылка
		|ИЗ
		|	КритерийОтбора.СвязанныеДокументы(&Документ) КАК КритерийОтбораСвязанныеДокументы";
	
	ДополнительныйТекстДляОснования =
	    "
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТекущийДокумент.Ссылка.Основание.Проведен
		|			ТОГДА 14
		|		КОГДА ТекущийДокумент.Ссылка.Основание.ПометкаУдаления
		|			ТОГДА 13
		|		ИНАЧЕ 12
		|	КОНЕЦ КАК Состояние,
		|	ТекущийДокумент.Ссылка.Основание КАК Ссылка
		|ИЗ
		|	&ИмяТекущегоДокумента КАК ТекущийДокумент
		|ГДЕ
		|	ТекущийДокумент.Ссылка = &Документ";		
	
	Если ЕстьРеквизитОснование Тогда
		ТекстЗапроса = ОсновнойТекст + ДополнительныйТекстДляОснования;
	Иначе
		ТекстЗапроса = ОсновнойТекст;
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти