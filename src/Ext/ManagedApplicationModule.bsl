
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль приложения
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

// Хранение структуры параметров приложения.
Перем глПараметрыПриложения Экспорт;

// Служебная глобальная переменная с переопределяемым значением.
Перем глПереопределяемаяПеременная Экспорт;

#КонецОбласти

#Область ОбработчикиСобытий

// Обработчик события При начале работы системы.
//
Процедура ПриНачалеРаботыСистемы()
	
	// Инициализация параметров приложения и установка заголовка приложения.
	СлужебныйВызовСервера.ИнициализироватьПараметрыПриложения(глПараметрыПриложения);
	УстановитьЗаголовокПриложения(глПараметрыПриложения.Заголовок);
		
КонецПроцедуры

// Обработчик события Перед завершением работы системы.
//
Процедура ПередЗавершениемРаботыСистемы(Отказ, ТекстПредупреждения)
	
	Если глПараметрыПриложения.ЗапрашиватьПодтверждение Тогда
		// Вывод вопроса о завершении работы с программой.
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Завершить работу с программой?'");	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обрабатывает завершение работы с окном с модифицированными данными; 
//  исключая ошибку серверного вызова при завершении работы с системой.
//	
//	Модуль формы-источника вызова должен содержать экспортную процедуру для обработки ответа на вопрос
//	о завершении работы с текущей модифицированной формой: ПроверитьМодифицированностьПередЗакрытиемПослеОтвета. 
//
Процедура ОбработатьЗавершениеРаботыСОкномСМодифицированнымиДанными() Экспорт
	
	// Проверка параметров процедуры.
	Если Не ТипЗнч(глПереопределяемаяПеременная) = Тип("ФормаКлиентскогоПриложения") Тогда
		ТекстИсключения = НСтр("ru = 'Неверное значение глобальной переменной глПереопределяемаяПеременная:
			|значение переменной не является типом ФормаКлиентскогоПриложения!'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ОписаниеОповещения 	= Новый ОписаниеОповещения("ПроверитьМодифицированностьПередЗакрытиемПослеОтвета", глПереопределяемаяПеременная);
	ТекстВопроса		= НСтр("ru = 'Данные были изменены. Сохранить изменения?'");		
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
	
	глПереопределяемаяПеременная = Неопределено;
	
КонецПроцедуры

#КонецОбласти
