
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события При создании на сервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НаименованиеОрганизации = Константы.НаименованиеОрганизации.Получить();
	ПрефиксОрганизации		= Константы.ПрефиксОрганизации.Получить();
	
КонецПроцедуры

// Обработчик события Перед закрытием.
//
&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ФормыКлиент.ПроверитьМодифицированностьПередЗакрытием(ЭтаФорма, Отказ);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команда Записать.
//
&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьДанные();
	
КонецПроцедуры

// Команда Записать и закрыть.
//
&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьДанные();
	Закрыть();
	
КонецПроцедуры

// Команда Параметры учета (подменю Дополнительно);
//  Открывает форму обработки Параметры учета.
//
&НаКлиенте
Процедура ОткрытьПараметрыУчета(Команда)
	
	ОткрытьФорму("Обработка.ПараметрыУчета.Форма.Форма");
	
КонецПроцедуры

// Команда Управление датами запрета редактирования (подменю Дополнительно);
//  Открывает форму обработки Управление датами запрета редактирования. 
//
&НаКлиенте
Процедура ОткрытьУправлениеДатамиЗапретаРедактирования(Команда)
	
	ОткрытьФорму("Обработка.УправлениеДатамиЗапретаРедактирования.Форма.Форма");	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Завершение выполнения обработчика ПередЗакрытием. 
//
&НаКлиенте
Процедура ПроверитьМодифицированностьПередЗакрытиемПослеОтвета(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаписатьДанные();
		Закрыть();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
    КонецЕсли;	
	
КонецПроцедуры

// Записывает указанные данные на форме в соответствующие константы.
//
&НаКлиенте
Процедура ЗаписатьДанные()
	
	ЗаписываемыеДанные = Новый Структура;
	ЗаписываемыеДанные.Вставить("НаименованиеОрганизации", 	НаименованиеОрганизации);
	ЗаписываемыеДанные.Вставить("ПрефиксОрганизации",		ПрефиксОрганизации);	
	ОбщегоНазначенияВызовСервера.УстановитьЗначенияКонстант(ЗаписываемыеДанные);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти