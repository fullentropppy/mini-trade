
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события При создании на сервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначениеВидимости 			= Не Параметры.Свойство("ДействиеИзНастроекПрограммы");
	Элементы.Шапка.Видимость 	= 	ЗначениеВидимости;
	ИспользоватьППД 			= ПолнотекстовыйПоискДанныхСервер.ИспользуетсяППД();
	
	ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события Использовать ППД При изменении.
//
&НаКлиенте
Процедура ИспользоватьППДПриИзменении(Элемент)
	
	ПереключитьИспользованиеППДНаСервере();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команда Обновить индекс.
//  Обновляет индекс полнотекстового поиска данных.
//
&НаКлиенте
Процедура ОбновитьИндекс(Команда)
	
	ОбновитьИндексНаСервере();
	
КонецПроцедуры

// Команда Очистить.
//  Очищает индекс полнотекстового поиска данных.
//
&НаКлиенте
Процедура ОчиститьИндекс(Команда)
	
	ОчиститьИндексНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет данные полнотекстового поиска данных и устанавливает условное оформление на форме.
//
&НаСервере
Процедура ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме()
	
	ИспользоватьППД = ПолнотекстовыйПоискДанныхСервер.ИспользуетсяППД();
	ПустаяДата		= глПустаяДата();
	
	Если ИспользоватьППД Тогда	
		ДатаАктуальности 	= ПолнотекстовыйПоиск.ДатаАктуальности();
		ИндексАктуален 		= ПолнотекстовыйПоиск.ИндексАктуален();
		ИндексКорректен		= ПолнотекстовыйПоиск.ПроверитьИндекс();
		Если ДатаАктуальности = ПустаяДата ИЛИ Не ИндексАктуален ИЛИ Не ИндексКорректен Тогда
			Состояние = НСтр("ru = 'Требуется обновление'");
		ИначеЕсли ИндексАктуален Тогда
			Состояние = НСтр("ru = 'Обновление не требуется'");
		КонецЕсли;
	Иначе
		ДатаАктуальности = ПустаяДата; 
		Состояние = НСтр("ru = 'Полнотекстовый поиск данных отключен'");
	КонецЕсли;
		
КонецПроцедуры

// Переключает использование индекса полнотекстового поиска данных.
//
&НаСервере
Процедура ПереключитьИспользованиеППДНаСервере();
	
	ПолнотекстовыйПоискДанныхСервер.ПереключитьИспользованиеППД(ИспользоватьППД);
	ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме();	
	
КонецПроцедуры

// Обновляет индекс полнотекстового поиска данных.
//
&НаСервере
Процедура ОбновитьИндексНаСервере();
	
	ПолнотекстовыйПоискДанныхСервер.ОбновитьИндексППД();
	ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме();
	
КонецПроцедуры

// Очищает индекс полнотекстового поиска данных.
//
&НаСервере
Процедура ОчиститьИндексНаСервере();
	
	ПолнотекстовыйПоискДанныхСервер.ОчиститьИндексППД();	
	ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме();
	
КонецПроцедуры

#КонецОбласти