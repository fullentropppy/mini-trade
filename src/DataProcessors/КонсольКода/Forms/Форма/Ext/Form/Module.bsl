
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события При открытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазделительЛоговВыполнения = ОбщегоНазначенияКлиентСервер.УмноженнаяСтрока("-", 120);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команда Выполнить;
//	Выполняет текст кода в выбранном контексте.
//
&НаКлиенте
Процедура ВыполнитьПроивольныйКод(Команда)
	
	Если ПустаяСтрока(ТекстКода) Тогда
		ТекстПредупреждения = НСтр( "ru = 'Отсутствует код к исполнению'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ДатаНачалаВыполнения	= ТекущаяДата();
	ВремяНачалаВыполнения 	= ТекущаяУниверсальнаяДатаВМиллисекундах();	
	
	Если ВыполнитьНаКлиенте Тогда
		РезультатВыполнения = ОбщегоНазначенияКлиент.ВыполнитьПроизвольныйКодНаКлиенте(ТекстКода);
	Иначе
		РезультатВыполнения = ОбщегоНазначенияВызовСервера.ВыполнитьПроизвольныйКодНаСервере(ТекстКода);
	КонецЕсли;
	
	ДатаОкончанияВыполнения		= ТекущаяДата();
	ВремяОкончанияВыполнения 	= ТекущаяУниверсальнаяДатаВМиллисекундах();
	ОбщееВремяВыполнения		= Окр((ВремяОкончанияВыполнения - ВремяНачалаВыполнения) / 1000, 2);
	
	Если РезультатВыполнения.Успешно Тогда
		ТекстЛога = НСтр("ru = 'Произвольный код успешно выполнен'");		
	Иначе
		ТекстЛога = НСтр("ru = 'Ошибка выполнения произвольного кода:
			|'") + РезультатВыполнения.ОписаниеОшибки;
	КонецЕсли;
	
	ТекстЛога = СтрШаблон("%1 - время выполнения: %2 с. (%3 - %4) 
		|	@ %5%6%7", 
		ТекущаяДата(), ОбщееВремяВыполнения, ДатаНачалаВыполнения, ДатаОкончанияВыполнения,
		ТекстЛога, Символы.ПС, РазделительЛоговВыполнения);
	ЛогВыполнений.ДобавитьСтроку(ТекстЛога);
	
КонецПроцедуры

// Команда Очистить лог выполнений;
//	Очищает текстовое поле лога выполнений текстов кода.
//
&НаКлиенте
Процедура ОчиститьЛогВыполнений(Команда)
	
	ЛогВыполнений.Очистить();
	
КонецПроцедуры

#КонецОбласти