
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события При создании на сервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДанныеНаФорме();	
		
КонецПроцедуры

// Обработчик события Перед закрытием.
//
&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ФормыКлиент.ПроверитьМодифицированностьПередЗакрытием(ЭтаФорма, Отказ);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команда Записать.
//
&НаКлиенте
Процедура Записать(Команда)
	
	Если Модифицированность Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Команда Записать и закрыть/
//
&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Модифицированность Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Завершение выполнения обработчика ПередЗакрытием. 
//
&НаКлиенте
Процедура ПроверитьМодифицированностьПередЗакрытиемПослеОтвета(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Если Модифицированность Тогда
			ЗаписатьНаСервере();
		КонецЕсли;
		
		Закрыть();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
    КонецЕсли;	
	
КонецПроцедуры

// Записывает данные на форме в констунту Общая дата запрета редактирования и регистр сведений Даты запрета редактирования.
//
&НаСервере
Процедура ЗаписатьНаСервере()
		
	Если Не ИсходнаяОбщаяДата = ОбщаяДата Тогда
		// Запись общей даты запрета редактирования.
		Константы.ОбщаяДатаЗапретаРедактирования.Установить(ОбщаяДата);
	КонецЕсли;
	
	// Запись персональных дат запрета редактирования.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&ОбщаяДата КАК ОбщаяДата,
		|	Список.Пользователь КАК Пользователь,
		|	Список.Дата КАК ПерсональнаяДата,
		|	Список.ПостоянныйДоступ КАК ПостоянныйДоступ
		|ПОМЕСТИТЬ втСписок
		|ИЗ
		|	&Список КАК Список
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСписок.ОбщаяДата КАК ОбщаяДата,
		|	втСписок.Пользователь КАК Пользователь,
		|	МИНИМУМ(втСписок.ПерсональнаяДата) КАК ПерсональнаяДата,
		|	МАКСИМУМ(втСписок.ПостоянныйДоступ) КАК ПостоянныйДоступ
		|ПОМЕСТИТЬ втСвернутыйСписок
		|ИЗ
		|	втСписок КАК втСписок
		|
		|СГРУППИРОВАТЬ ПО
		|	втСписок.ОбщаяДата,
		|	втСписок.Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСвернутыйСписок.Пользователь ЕСТЬ NULL
		|		ИЛИ втСвернутыйСписок.ПостоянныйДоступ = ЛОЖЬ
		|			И втСвернутыйСписок.ПерсональнаяДата = втСвернутыйСписок.ОбщаяДата КАК УдалитьЗапись,
		|	ЕСТЬNULL(втСвернутыйСписок.Пользователь, ДатыЗапретаРедактирования.Пользователь) КАК Пользователь,
		|	втСвернутыйСписок.ПерсональнаяДата КАК Дата,
		|	втСвернутыйСписок.ПостоянныйДоступ КАК ПостоянныйДоступ
		|ИЗ
		|	втСвернутыйСписок КАК втСвернутыйСписок
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаРедактирования КАК ДатыЗапретаРедактирования
		|		ПО втСвернутыйСписок.Пользователь = ДатыЗапретаРедактирования.Пользователь
		|ГДЕ
		|	НЕ(ЕСТЬNULL(втСвернутыйСписок.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) = ЕСТЬNULL(ДатыЗапретаРедактирования.Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
		|				И ЕСТЬNULL(втСвернутыйСписок.ПерсональнаяДата, ДАТАВРЕМЯ(1, 1, 1)) = ЕСТЬNULL(ДатыЗапретаРедактирования.Дата, ДАТАВРЕМЯ(1, 1, 1))
		|				И ЕСТЬNULL(втСвернутыйСписок.ПостоянныйДоступ, ЛОЖЬ) = ЕСТЬNULL(ДатыЗапретаРедактирования.ПостоянныйДоступ, ЛОЖЬ))";
	
	Запрос.УстановитьПараметр("Список", 	Список.Выгрузить());
	Запрос.УстановитьПараметр("ОбщаяДата", 	ОбщаяДата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДатыЗапретаРедактированияСервер.ОбновитьДатыИзВыборкиИзменений(Выборка);
	
	ЗаполнитьДанныеНаФорме();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

// Заполняет данные дат запрета редактирования на форме.
//
&НаСервере
Процедура ЗаполнитьДанныеНаФорме() 

	ОбщаяДата 			= ДатыЗапретаРедактированияСервер.ОбщаяДата();
	ИсходнаяОбщаяДата	= ОбщаяДата;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыЗапретаРедактирования.Пользователь КАК Пользователь,
		|	ДатыЗапретаРедактирования.Дата КАК Дата,
		|	ДатыЗапретаРедактирования.ПостоянныйДоступ КАК ПостоянныйДоступ
		|ИЗ
		|	РегистрСведений.ДатыЗапретаРедактирования КАК ДатыЗапретаРедактирования";
	
	ДатыЗапретаРедактирования = Запрос.Выполнить().Выгрузить();
	Список.Загрузить(ДатыЗапретаРедактирования);
	
КонецПроцедуры

#КонецОбласти