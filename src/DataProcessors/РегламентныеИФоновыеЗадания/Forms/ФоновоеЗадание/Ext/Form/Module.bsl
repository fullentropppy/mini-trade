
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

// Обработчик события При создании на сервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Идентификатор") Тогда
		ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме();
	Иначе
		ТекстИсключения = НСтр("ru = 'Не указан идентификатор фонового задания!'"); 
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Обработчик события Представление регламентного задания открытие.
//
&НаКлиенте
Процедура ПредставлениеРегламентногоЗаданияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) Тогда
		ПараметрыФормы = Новый Структура("Идентификатор", ИдентификаторРегламентногоЗадания);
		ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентноеЗадание", ПараметрыФормы);						
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команда Закрыть.
//
&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

// Заполняет данные фонового задания и устанавливает условное оформление на форме.
//
&НаСервере
Процедура ЗаполнитьДанныеИУстановитьУсловноеОформлениеНаФорме()
	
	// Заполнение реквизитов формы из данных фонового задания.
	ФоновоеЗадание			= ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Параметры.Идентификатор);
	Наименование			= ФоновоеЗадание.Наименование;
	ИмяМетода				= ФоновоеЗадание.ИмяМетода;
	Идентификатор			= ФоновоеЗадание.УникальныйИдентификатор;
	Ключ					= ФоновоеЗадание.Ключ;
	Расположение			= ФоновоеЗадание.Расположение;
	ДатаНачалаВыполнения	= ФоновоеЗадание.Начало;
	ДатаОкончанияВыполнения	= ФоновоеЗадание.Конец;
	
	// Данные регламентного задания.
	РегламентноеЗадание = ФоновоеЗадание.РегламентноеЗадание;
	Если РегламентноеЗадание = Неопределено Тогда
		Элементы.ПредставлениеРегламентногоЗадания.КнопкаОткрытия = Ложь;
	Иначе
		НаименованиеРегламентногоЗадания 	= РегламентноеЗадание.Метаданные.Наименование;
		ПредставлениеРегламентногоЗадания 	= ?(ПустаяСтрока(НаименованиеРегламентногоЗадания), 
			РегламентноеЗадание.Метаданные.Имя, НаименованиеРегламентногоЗадания);
		ИдентификаторРегламентногоЗадания	= РегламентноеЗадание.УникальныйИдентификатор;
	КонецЕсли;
		
	// Информация об ошибке.
	ИнформацияОбОшибке = ФоновоеЗадание.ИнформацияОбОшибке;
	Если ИнформацияОбОшибке = Неопределено Тогда
		Элементы.СтраницаИнформацииОбОшибке.Доступность = Ложь;
	Иначе
		Элементы.СтраницаИнформацииОбОшибке.Картинка = БиблиотекаКартинок.Предупреждение;	
		// Заполнение информации об ошибке последнего фонового задания.
		ИнформацияОбОшибкеМодуль 	= СтрШаблон("%1 (%2)", ИнформацияОбОшибке.ИмяМодуля, ИнформацияОбОшибке.НомерСтроки);
		ИнформацияОбОшибкеСтрока	= ИнформацияОбОшибке.ИсходнаяСтрока;
		ИнформацияОбОшибкеОписание	= ИнформацияОбОшибке.Описание;
		ИнформацияОбОшибкеПричина	= КраткоеПредставлениеОшибки(ИнформацияОбОшибке.Причина);			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
