
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиКомандФормы

// Команда Получить УИД;
//  Выводит уникальный идентификатор указанной ссылки.
//
&НаКлиенте
Процедура ПолучитьУИД(Команда)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		УИД = ОбщегоНазначенияВызовСервера.УникальныйИдентификаторСсылкиОбъекта(Ссылка, Истина);	
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Укажите ссылку объекта для получения уникального идентификатора'");
		ПоказатьПредупреждение(, ТекстПредупреждения);		
	КонецЕсли;
	
КонецПроцедуры

// Команда Получить ссылку;
//  Выполняет поиск ссылки объекта по указанному уникальному идентификатору.
//
&НаКлиенте
Процедура ПолучитьСсылку(Команда)
	
	УИДУказан = ЗначениеЗаполнено(УИД);
	Если УИДУказан И УникальныйИдентификаторИмеетКорректнуюСтруктуру() Тогда
		СсылкаПоУИД = ОбщегоНазначенияВызовСервера.СсылкаОбъектаПоУникальномуИдентификатору(УИД);
		
		// Присвоение реквизиту СсылкаОбъекта полученное значения в любом случае,
		// даже если ссылка объекта по уникальному идентификатору не найдена.
		Ссылка = СсылкаПоУИД;
		Если Не ЗначениеЗаполнено(СсылкаПоУИД) Тогда
			ТекстПредупреждения = НСтр("ru = 'Ссылка объекта по указанному уникальному идентификатору не найдена'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	ИначеЕсли Не УИДУказан Тогда
		ТекстПредупреждения = НСтр("ru = 'Укажите уникальный идентификатор для поиска ссылки объекта'");
		ПоказатьПредупреждение(, ТекстПредупреждения);		
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Указанный уникальный идентификатор имеет некорректную стуктуру'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет корректность структуры уникального идентификатор указанного на форме. 
// 
// Возвращаемое значение:
//   - Булево
//
&НаКлиенте
Функция УникальныйИдентификаторИмеетКорректнуюСтруктуру()
	
	Попытка
		НовыйУИД				= Новый УникальныйИдентификатор(УИД);
		ЭтоКорректнаяСтруктура 	= Не НовыйУИД = Неопределено;
	Исключение
	    ЭтоКорректнаяСтруктура = Ложь;
	КонецПопытки;
	
	Возврат ЭтоКорректнаяСтруктура;
	
КонецФункции

#КонецОбласти