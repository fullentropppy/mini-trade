
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль команды
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

// Обработчик события Обработка команды/
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	// Формирование передаваемых параметров в общую форму ФормаПечати.
	ТабличныйДокумент 		= ТабличныйДокументУниверсальнаяНакладная(ПараметрКоманды);
	ТипЗнчПараметраКоманды 	= ТипЗнч(ПараметрКоманды);
	
	Если ТипЗнчПараметраКоманды = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
	    Заголовок = НСтр("ru = 'Печать приходной накладной'");
	ИначеЕсли ТипЗнчПараметраКоманды = Тип("ДокументСсылка.ПродажаТоваров") Тогда
	    Заголовок = НСтр("ru = 'Печать расходной накладной'"); 
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;	
	ПараметрыФормы.Вставить("Заголовок", 			Заголовок);
	ПараметрыФормы.Вставить("ТабличныйДокумент", 	ТабличныйДокумент);
	ПараметрыФормы.Вставить("КоличествоКопий",		1);
	
	// Открытие общей формы ФормаПечати с параметрами.
	ОткрытьФорму("ОбщаяФорма.ФормаПечати", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает сформированный табличный документ.
//
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров/ПродажаТоваров - ссылка печатаемого документа.
// 
// Возвращаемое значение:
//   - ТабличныйДокумент
//
&НаСервере
Функция ТабличныйДокументУниверсальнаяНакладная(Документ) Экспорт
	
	ПроверитьНаличиеПраваПечататьНакладную(Документ);
	
	ТабличныйДокумент = Обработки.ПечатьНакладной.ПечатьУниверсальнаяНакладная(Документ);
	Возврат ТабличныйДокумент;
	
КонецФункции

// Проверяет наличие права Изменение по печатаемогу документу.
//
// Параметры:
//  Документ - ДокументСсылка.ПоступлениеТоваров/ПродажаТоваров - ссылка печтаемого документа. 
//
&НаСервере
Процедура ПроверитьНаличиеПраваПечататьНакладную(Документ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущийПользовательИБ 	= ПользователиСервер.ТекущийПользовательИБ();		
	Право 					= "Изменение";
	
	ТипЗнчНакладной = ТипЗнч(Документ);
	Если ТипЗнчНакладной = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ЕстьПравоПечати = ПравоДоступа(Право, Метаданные.Документы.ПоступлениеТоваров, ТекущийПользовательИБ);
		ИмяНакладной	= НСтр("ru = 'поступления товаров'");
	ИначеЕсли ТипЗнчНакладной = Тип("ДокументСсылка.ПродажаТоваров") Тогда
		ЕстьПравоПечати = ПравоДоступа(Право, Метаданные.Документы.ПродажаТоваров, ТекущийПользовательИБ);
		ИмяНакладной	= НСтр("ru = 'продажи товаров'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Если Не ЕстьПравоПечати Тогда
		ТекстИсключения = СтрШаблон(НСтр("ru = 'Недостаточно прав для печати %1!'"), ИмяНакладной);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
