
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль объекта
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

// Обработчик события Перед записью.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа И НаименованиеКатегорииНеУникально() Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Наименование категории ошибок ""%1"" не уникально'"), Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет наименование категории ошибок на уникальность.
// 
// Возвращаемое значение:
//   - Булево 
//
Функция НаименованиеКатегорииНеУникально()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА КАК НаименованиеНеУникально
		|ИЗ
		|	Справочник.КатегорииОшибок КАК КатегорииОшибок
		|ГДЕ
		|	КатегорииОшибок.Наименование = &ЭтоНаименование
		|	И НЕ КатегорииОшибок.Ссылка = &ЭтаКатегория";
	
	Запрос.УстановитьПараметр("ЭтоНаименование", 	Наименование);
	Запрос.УстановитьПараметр("ЭтаКатегория", 		Ссылка);
	
	НаименованиеНеУникально = Не Запрос.Выполнить().Пустой();
	Возврат НаименованиеНеУникально;	
	
КонецФункции

#КонецОбласти