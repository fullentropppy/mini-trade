
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль объекта
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

// Обработчик события Перед записью.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа И Не ИмяПользователяУникально() Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Имя пользователя ""%1"" не уникально'"), Наименование);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет имя пользователя на уникальность.
// 
// Возвращаемое значение:
//   - Булево 
//
Функция ИмяПользователяУникально()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ИмяНеУникально
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Наименование = &ЭтоНаименование
		|	И НЕ Пользователи.Ссылка = &ЭтотПользователь";
	
	Запрос.УстановитьПараметр("ЭтоНаименование", 	Наименование);
	Запрос.УстановитьПараметр("ЭтотПользователь", 	Ссылка);
	
	ИмяУникально = Запрос.Выполнить().Пустой();
	Возврат ИмяУникально;	
	
КонецФункции

#КонецОбласти