
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль формы
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем ДоменыИПользователи;

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Обработчик события При открытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПользователиОС		= ПользователиОС();
	ДоменыИПользователи = Новый ФиксированныйМассив(ПользователиОС);	
	
	ОбновитьСписокДоменов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

// Обработчик события Домены при активизации строки.
//
&НаКлиенте
Процедура ДоменыПриАктивизацииСтроки(Элемент)
	
	ПользователиДомена.Очистить();
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТекущегоДомена = Элемент.ТекущиеДанные.Имя;	
	Для Каждого Домен Из ДоменыИПользователи Цикл
		Если Домен.ИмяДомена = ИмяТекущегоДомена Тогда
			// Заполнение списка пользователей выбранного домена.
			Для Каждого Пользователь Из Домен.Пользователи Цикл
				ПользовательДомена 		= ПользователиДомена.Добавить();
				ПользовательДомена.Имя 	= Пользователь;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;		
		
	ПользователиДомена.Сортировать("Имя");
	
КонецПроцедуры

// Обработчик события Пользователи домена выбор.
//
&НаКлиенте
Процедура ПользователиДоменаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СкомпоноватьРезультатИЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Команда Выбрать.
//
&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекстПредупреждения = "";
	
	Если Элементы.Домены.ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Выберите домен'"); 
	ИначеЕсли Элементы.ПользователиДомена.ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Выберите пользователя домена'");
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстПредупреждения) Тогда
		СкомпоноватьРезультатИЗакрытьФорму();
	Иначе
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновляет список Домены.
//
&НаКлиенте
Процедура ОбновитьСписокДоменов()
	
	Домены.Очистить();
	
	Для Каждого Домен Из ДоменыИПользователи Цикл
		НоваяСтрока 	= Домены.Добавить();	
	   	НоваяСтрока.Имя = Домен.ИмяДомена;
	КонецЦикла;
	
	Домены.Сортировать("Имя");
	
КонецПроцедуры

// Формирует полное именя пользователя ОС из выбранного домена и пользователя домена.
//
&НаКлиенте
Процедура СкомпоноватьРезультатИЗакрытьФорму()
	
	РезультатВыбора = СтрШаблон("\\%1\%2", Элементы.Домены.ТекущиеДанные.Имя, Элементы.ПользователиДомена.ТекущиеДанные.Имя);	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти
