
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Модуль команды
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

// // Обработчик события Обработка команды.
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ЕстьОткрытыеЗакрываемыеОкна() Тогда
		ОписаниеОповещения 	= Новый ОписаниеОповещения("ОбработкаКомандыПослеОтвета", ЭтотОбъект);
		ТекстВопроса		= НСтр("ru = 'Все открытые окна в приложении будут закрыты. Продолжить?'");		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Нет открытых окон приложения для закрытия'");	
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Завершение выполнения обработчика ОбработкаКоманды. 
//
&НаКлиенте
Процедура ОбработкаКомандыПослеОтвета(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытыеОкна = ПолучитьОкна();
	Для Каждого Окно Из ОткрытыеОкна Цикл
		ФормаОкна = Окно.ПолучитьСодержимое();
		Если Не ФормаОкна = Неопределено И ТипЗнч(ФормаОкна) = Тип("УправляемаяФорма") Тогда
	    	ФормаОкна.Закрыть();
		КонецЕсли;    
	КонецЦикла;	
	
КонецПроцедуры

// Проверяет наличие открытых окно, которые могут быть закрытыми.
//  Окно может быть закрытым есть не является основным и начальной страницей.
// 
// Возвращаемое значение:
//   - Булево
//
&НаКлиенте
Функция ЕстьОткрытыеЗакрываемыеОкна()
	
	ЕстьОткрытыеЗакрываемыеОкна = Ложь;
	
	ОткрытыеОкна = ПолучитьОкна();	
	Для Каждого Окно Из ОткрытыеОкна Цикл
		Если Не Окно.Основное И Не Окно.НачальнаяСтраница Тогда
			ЕстьОткрытыеЗакрываемыеОкна = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьОткрытыеЗакрываемыеОкна;
	
КонецФункции

#КонецОбласти