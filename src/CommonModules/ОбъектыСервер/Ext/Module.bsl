
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Общий модуль ОбъектыСервер
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает автора документа из значения параметра сеанса ТекущийПользователь. 
//
// Параметры:
//  Автор - СправочникСсылка.Пользователи - реквизит объекта, хранящий ссылку пользователя автора. 
//
Процедура УстановитьАвтораДокумента(Автор) Экспорт
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры

// Устанавливает склад из значения персональной настройки ОсновнойСклад.
//
// Параметры:
//  Склад						 - СправочникСсылка.Склады	 - реквизит объекта, хранящий ссылку склада;
//  УстановитьПринудительно	 - Булево					 - принудительно установить склад, если он уже установлен.
//
Процедура УстановитьОсновнойСклад(Склад, УстановитьПринудительно = Ложь) Экспорт
	
	Если Не УстановитьПринудительно И ЗначениеЗаполнено(Склад) Тогда
		// Отмена операции, если это не принудительная установка склада
		// и склад уже заполнен (при копировании).
		Возврат;				
	КонецЕсли;
	
	Склад = ПерсональныеНастройкиСервер.ЗначениеОсновнойСклад();
	
КонецПроцедуры

// Устанавливает вид цены из значения персональной настройки ОсновнойВидЦенПоступленияТоваров или ОсновнойВидЦенПродажаТоваров.
//
// Параметры:
//  ФормаОбъекта			- ФормаКлиентскогоПриложения 	- форма обрабатываемого объекта;
//  УстановитьПринудительно	- Булево						- принудительно установить вид цены, если он уже установлен.
//
Процедура УстановитьВидЦеныОперации(ФормаОбъекта, УстановитьПринудительно = Ложь) Экспорт
	
	Объект = ФормаОбъекта.Объект;
	
	Если Не УстановитьПринудительно И ЗначениеЗаполнено(Объект.ВидЦены) Тогда
		// Отмена операции, если это не принудительная установка вида цены
		// и вид цены уже заполнен (при копировании).
		Возврат;				
	КонецЕсли;
	
	ТипЗнчСсылки = ТипЗнч(Объект.Ссылка);
	Если ТипЗнчСсылки = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		ВидЦены = ПерсональныеНастройкиСервер.ЗначениеОсновнойВидЦенПоступленияТоваров();
	ИначеЕсли ТипЗнчСсылки = Тип("ДокументСсылка.ПродажаТоваров") Тогда
		ВидЦены = ПерсональныеНастройкиСервер.ЗначениеОсновнойВидЦенПродажаТоваров();
	Иначе
		ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
	КонецЕсли;
	
	Объект.ВидЦены = ВидЦены;
	
КонецПроцедуры

#КонецОбласти