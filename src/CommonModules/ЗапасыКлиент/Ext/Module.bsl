
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Общий модуль ЗапасыКлиент
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Показывает вопрос на форме документа с предупреждением перед проведением без контроля остатков;
//
//	Модуль формы-источника вызова должен содержать параметр записи ПроведениеБезКонтроляОстатков
//	в процедуре ПроведениеБезКонтроляОстатков. Параметр необходимо обработать в модуле объекта документа.
//
// Параметры:
//  ФормаДокумента - ФормаКлиентскогоПриложения - форма документа из которой будет вызван метод Записать() с параметрами записи. 
//
Процедура ПровестиДокументБезКонтроляОстатков(ФормаДокумента) Экспорт
	
	ДополнительныеПараметры = Новый Структура("ФормаДокумента", ФормаДокумента);
	ОписаниеОповещения		= Новый ОписаниеОповещения("ПровестиДокументБезКонтроляОстатковПослеОтвета", ЗапасыКлиент, ДополнительныеПараметры);
	ТекстВопроса 			= НСтр("ru = 'При проведении документа без контроля свободных остатков товаров на складе списания "
		+ "могут образоваться отрицательные остатки. Продолжить?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Завершение выполнения обработчика ПровестиДокументБезКонтроляОстатков. 
//
Процедура ПровестиДокументБезКонтроляОстатковПослеОтвета(РезультатОтвета, ДополнительныеПараметры) Экспорт
	
	Если РезультатОтвета = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	// Создание параметров записи документа.
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	ПараметрыЗаписи.Вставить("ПроведениеБезКонтроляОстатков");
	
	// Запись документа с параметрами записи.
	ФормаДокумента = ДополнительныеПараметры.ФормаДокумента;
	ФормаДокумента.Записать(ПараметрыЗаписи);
		
КонецПроцедуры

#КонецОбласти