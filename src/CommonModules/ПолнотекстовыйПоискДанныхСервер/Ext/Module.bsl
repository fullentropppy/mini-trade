
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Общий модуль ПолнотекстовыйПоискДанных
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает использование полнотекстового поиска данных.
//
// Параметры:
//  Значение - Булево - значение использования.
//
Процедура ПереключитьИспользованиеППД(Значение) Экспорт
	
	РежимППД = ?(Значение, РежимПолнотекстовогоПоиска.Разрешить, РежимПолнотекстовогоПоиска.Запретить);
	ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимППД);	
	
	// Переключение использования регламентных заданий в соответствии с устанавливаемым значением.
	// Регламентные задания:
	// * Обновление индекса полнотекстового поиска данных;
	// * Слияние индексов полнотекстового поиска данных.
	
	// Обновление индекса полнотекстового поиска данных. 
	МетаданныеРегламентногоЗадания = Метаданные.РегламентныеЗадания.ОбновлениеИндексаППД;
	ОбщегоНазначенияСервер.ПереключитьИспользованиеРегламентногоЗадания(МетаданныеРегламентногоЗадания, Значение);

	// Слияние индексов полнотекстового поиска данных. 
	МетаданныеРегламентногоЗадания = Метаданные.РегламентныеЗадания.СлияниеИндексовППД;
	ОбщегоНазначенияСервер.ПереключитьИспользованиеРегламентногоЗадания(МетаданныеРегламентногоЗадания, Значение);

КонецПроцедуры

// Обновляет дополнительный индекс полнотекстового поиска данных.
//
Процедура ОбновитьИндексППД() Экспорт
	
	ВыполнитьДействиеСИндексомППД(Ложь, Истина);
	
КонецПроцедуры

// Объединяет дополнительный и основной индекс полнотекстового поиска данных.
//
Процедура ОбъединитьИндексыППД() Экспорт
	
	ВыполнитьДействиеСИндексомППД(Истина, Ложь);
	
КонецПроцедуры

// Очищает индекс полнотекстового поиска данных.
//
Процедура ОчиститьИндексППД() Экспорт
	
	Если ИспользуетсяППД() Тогда
		ПолнотекстовыйПоиск.ОчиститьИндекс();
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак использования полнотекстового поиска данных.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ИспользуетсяППД() Экспорт
	
	РежимПП 		= ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска();
	ИспользуетсяПП 	= РежимПП = РежимПолнотекстовогоПоиска.Разрешить;
	
	Возврат ИспользуетсяПП;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет действия над индексом полнотекстового поиска данных.
//
// Параметры:
//  РазрешитьСлияние	- Булево - выполнить слияние индексов;  
//  Порционное			- Булево - обновить индекс. 
//
Процедура ВыполнитьДействиеСИндексомППД(РазрешитьСлияние, Порционное)
	
	ИспользуетсяППД = ИспользуетсяППД();
	Если ИспользуетсяППД Тогда
		Если Не ПолнотекстовыйПоиск.ИндексАктуален() Тогда
			ПолнотекстовыйПоиск.ОбновитьИндекс(РазрешитьСлияние, Порционное);
		КонецЕсли;
	Иначе
		// Запись в информацию об ошибках о невыполнении действия с обновление дат запрета редактирования.
		Уровень 	= Перечисления.УровниОшибок.Тривиальный;
		Категория	= Справочники.КатегорииОшибок.ППД_НеиспользуемыйФункционал;
		Описание	= НСтр("ru = 'Использование полнотекстового поиска данных выключено.
			|Действие над индексом полнотекстового поиска данных не выполнено.'");
		ИнформацияОбОшибкахСервер.ЗарегистрироватьОшибку(Уровень, Категория, Описание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти