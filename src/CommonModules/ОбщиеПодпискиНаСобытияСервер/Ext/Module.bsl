
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Общий модуль ОбщиеПодпискиНаСобытияСервер
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет общие для всех источников-документов действия при заполнении.
//
Процедура ВыполнитьОбщиеДействияПриЗаполненииДокумента(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
		
	Если Источник.ОбменДанными.Загрузка 
	 ИЛИ Не Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыСервер.УстановитьАвтораДокумента(Источник.Автор);
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		ЗаполнитьИсточникНаОснованииДанныхЗаполнения(Источник, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет общие для всех источников-документов действия при копировании.
//
Процедура ВыполнитьОбщиеДействияПриКопированииДокумента(Источник, ОбъектКопирования) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыСервер.УстановитьАвтораДокумента(Источник.Автор);
	
КонецПроцедуры

// Выполняет частные действия для источников-документов при заполнении.
//
Процедура ВыполнитьЧастныеДействияПриЗаполненииДокумента(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Источник.ОбменДанными.Загрузка
	 ИЛИ Не Источник.ЭтоНовый() Тогда
		Возврат;		
	КонецЕсли;
	
	УстановитьСкладПоУмолчанию(Источник);
	
КонецПроцедуры

// Выполняет частные действия для источников-документов при копировании.
//
Процедура ВыполнитьЧастныеДействияПриКопированииДокумента(Источник, ОбъектКопирования) Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСкладПоУмолчанию(Источник);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет источник на основании данных заполнения.
//
// Параметры:
//  Источник		 - ДокументОбъект - заполняемый документ;
//  ДанныеЗаполнения - ДокументСсылка - документ, содержащий данные для заполнения источника. 
//
Процедура ЗаполнитьИсточникНаОснованииДанныхЗаполнения(Источник, ДанныеЗаполнения)
	
	ТипЗнчИсточника 		= ТипЗнч(Источник);
	ТипЗнчДанныхЗаполнения 	= ТипЗнч(ДанныеЗаполнения);
	
	ИменаРеквизитовДанныхЗаполнения = Новый Массив;
	ИменаРеквизитовДанныхЗаполнения.Добавить("Товары");
	
	ИменаКолонокТоваров = Новый Массив;
	ИменаКолонокТоваров.Добавить("Товар");
	
	// Определение параметров заполнения по типу источника и данных заполнения.
	Если ТипЗнчИсточника = Тип("ДокументОбъект.УстановкаЦенТоваров") Тогда
		ЗаполнятьСклад 		= Ложь;
		ЗаполнятьВидЦены 	= Истина;
		
		ИменаРеквизитовДанныхЗаполнения.Добавить("ВидЦены");
		ИменаКолонокТоваров.Добавить("Цена");	
	Иначе
		ЗаполнятьСклад 			= Истина;
		ЗаполнятьВидЦены 		= Ложь;

		СкладИсточника 			= ?(ТипЗнчИсточника = Тип("ДокументОбъект.ПеремещениеТоваров"), "СкладОтправитель", "Склад");
		СкладДанныхЗаполнения	= ?(ТипЗнчДанныхЗаполнения = Тип("ДокументСсылка.ПеремещениеТоваров"), "СкладПолучатель", "Склад");
		
		ИменаРеквизитовДанныхЗаполнения.Добавить(СкладДанныхЗаполнения);
		ИменаКолонокТоваров.Добавить("Количество");
	КонецЕсли;
	
	ЗначенияРеквизитовДанныхЗаполнения = ОбщегоНазначенияСервер.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, ИменаРеквизитовДанныхЗаполнения);
		
	// Заполнение шапки.
	Источник.Основание = ДанныеЗаполнения;

	Если ЗаполнятьСклад Тогда
		Источник[СкладИсточника] = ЗначенияРеквизитовДанныхЗаполнения[СкладДанныхЗаполнения];
	КонецЕсли;
	
	Если ЗаполнятьВидЦены Тогда
		Источник.ВидЦены = ЗначенияРеквизитовДанныхЗаполнения.ВидЦены;	
	КонецЕсли;
		
	// Заполнение табличной части ЦеныТоваров.  
	ТоварыДанныхЗаполнения 	= ЗначенияРеквизитовДанныхЗаполнения.Товары.Выгрузить();
	ТоварыДляЗаполнения		= ОбщегоНазначенияСервер.ЗначенияКолонокТаблицыЗначений(ТоварыДанныхЗаполнения, ИменаКолонокТоваров);
	
	Источник.Товары.Загрузить(ТоварыДляЗаполнения);	
	
КонецПроцедуры

// Проверяет необходимость и устанавливает склад по умолчанию в документе, где это применимо.
//
// Параметры:
//  Документ - ДокументОбъект - обрабатываемый документ. 
//
Процедура УстановитьСкладПоУмолчанию(Документ)
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетПоСкладам") Тогда
		Документ.Склад = Справочники.Склады.ОсновнойСклад;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти