
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// © Гриценко Даниил 2021-2023г. | Общий модуль ПечатьСервер
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Универсальная функция формирования табличного документа из универсального запроса.
//
//  Требования к запросу:
//	 * все необходимые параметры должны быть установлены до вызова данной функции. Не должно быть параметров с именем Автор;
//	 * имя документа в тексте запросе должно быть Документ (например: вместо ОприходованиеТоваров использовать Документ);
//	 * имена полей в тексте запросе должы соответствовать именам параметров в макете;
//	 * в тексте запроса допускаютсятся следующие имена полей, которые будут модифицированы:
//	   * &Автор - будет заменено на поле из документа или на значение персональной настройки ФИО для печати, если настройка заполнена.
//
//	Требования к макету:
//	 * Макет может содержать разный набор областей, но допускаются только следующие имена областей:
//	   Заголовок, Шапка, ТоварыШапка, Товары, Подвал.
//
// Параметры:
//  Запрос - Запрос 			- запрос данных документа для заполнения табличного документа.
//								  См. требования к запросу в описании функции.
//  Макет - ТабличныйДокумент 	- макет формируемого табличного документа.
//								  См. требования к макету в описании функции.
// 
// Возвращаемое значение:
//   - ТабличныйДокумент 
//
Функция ТабличныйДокументИзУниверсальногоЗапроса(Запрос, Макет) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	// Обработка запроса.
	МодифицироватьТекстЗапроса(Запрос);
	
	// Получение данных документа.
	Выборка = Запрос.Выполнить().Выбрать();	
	Если Не Выборка.Следующий() Тогда
		ТекстИсключения = НСтр("ru = 'Неверный параметр Запрос:
			|результат запроса пустой!'");	
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	// Заполнение табличного документа данными документа.
	ЗаполнитьИВывестиОбластьМакета("Заголовок", Макет, Выборка, ТабличныйДокумент);
	ЗаполнитьИВывестиОбластьМакета("Шапка", Макет, Выборка, ТабличныйДокумент);
	ЗаполнитьИВывестиОбластьМакетаТовары(Макет, Выборка, ТабличныйДокумент);
	ЗаполнитьИВывестиОбластьМакета("Подвал", Макет, Выборка, ТабличныйДокумент);
	
	Возврат ТабличныйДокумент;	
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Модифицирует текст запроса.
//
//	Допустимые имена полей, которые будут модифицированы:
//	 * Автор - будет заменено на поле из документа или на значение персональной настройки 
//	   ФИО для печати, если настройка заполнена. 
//
// Параметры:
//  Запрос - Запрос - запрос, содержаший модифицируемый текст запроса.
//
Процедура МодифицироватьТекстЗапроса(Запрос)
	
	ТекстЗапроса = Запрос.Текст;
	
	Если СтрНайти(ТекстЗапроса, "&Автор") Тогда
		// Поле Автор.
		ФИОДляПечати = ПерсональныеНастройкиСервер.ЗначениеФИОДляПечати();
		ФИОДляПечати = ?(ПустаяСтрока(ФИОДляПечати), "Документ.Автор", СтрШаблон("""%1""", ФИОДляПечати));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Автор", ФИОДляПечати);
	КонецЕсли;
	
	// Обновление текста запроса.
	Запрос.Текст = ТекстЗапроса;
	
КонецПроцедуры

// Заполняет область макета данными выборки из запроса и выводит в табличный документ.
//
// Параметры:
//  ИмяОбласти			- Строка						- имя заполняемой области макета;
//  Макет				- ТабличныйДокумент 			- макет, содержащий заполняемую область; 
//  ВыборкаДанных		- ВыборкаИзРезультатаЗапроса	- данные заполнения области; 
//  ТабличныйДокумент	- ТабличныйДокумент 			- табличный документ, в который будет выведена заполненная область.
//
Процедура ЗаполнитьИВывестиОбластьМакета(ИмяОбласти, Макет, ВыборкаДанных, ТабличныйДокумент)
	
	ОбластьСуществует = Не Макет.Области.Найти(ИмяОбласти) = Неопределено;
	Если Не ОбластьСуществует Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
	ОбластьМакета.Параметры.Заполнить(ВыборкаДанных);
	ТабличныйДокумент.Вывести(ОбластьМакета);
		
КонецПроцедуры

// Заполняет область макета ТоварыШапка и Товары данными выборки из запроса и выводит в табличный документ.
//
// Параметры:
//  Макет				- ТабличныйДокумент 			- макет, содержащий область ТоварыШапка и Товары; 
//  ВыборкаДанных		- ВыборкаИзРезультатаЗапроса	- данные заполнения областей; 
//  ТабличныйДокумент	- ТабличныйДокумент 			- табличный документ, в который будут выведены заполненные области.
//
Процедура ЗаполнитьИВывестиОбластьМакетаТовары(Макет, ВыборкаДанных, ТабличныйДокумент)
	
	ЕстьТоварыШапка = Не Макет.Области.Найти("ТоварыШапка") = Неопределено;
	ЕстьТовары		= Не Макет.Области.Найти("Товары") = Неопределено;
	
	Если Не (ЕстьТоварыШапка И ЕстьТовары) Тогда
		Возврат;
	КонецЕсли;
	
	// Шапка товаров.
	ОбластьТоварыШапка = Макет.ПолучитьОбласть("ТоварыШапка");
	ТабличныйДокумент.Вывести(ОбластьТоварыШапка);
	
	// Список товаров.
	ОбластьТовары = Макет.ПолучитьОбласть("Товары");				
	ВыборкаТовары = ВыборкаДанных.Товары.Выбрать();
	
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти